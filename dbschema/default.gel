module default {
    # Enums
    # NOTE: Revoked is necessary to maintain data for users without access
    scalar type AuthStatus extending enum<pending, allowed, denied, revoked>;
    # NOTE: access_agent is not used now
    scalar type ResourceType extending enum<access_identity, create_agent_from_template, access_agent>;

    # User record based on Telegram User model
    # https://core.telegram.org/bots/api#user
    type User {
        # Telegram fields
        required telegram_id: int64 {
            constraint exclusive;
        }
        required is_bot: bool {
            default := false;
        }
        required first_name: str;
        last_name: str;
        username: str;
        language_code: str;

        # Tracking fields
        required first_seen: datetime {
            default := datetime_of_transaction();
        }

        # Computed property for display name
        property full_name := .first_name ++ ((' ' ++ .last_name) if exists .last_name else '');

        # Indexes for common queries
        index on (.telegram_id);
        index on (.username);
    }

    # Authorization record - separate from User for clean separation of concerns
    type Identity {
        required user: User {
            constraint exclusive;
        }

        # Letta identity reference (UUID string)
        required identifier_key: str {
            constraint exclusive;
        }
        required identity_id: str {
            constraint exclusive;
        }

        selected_agent: str;

        # Audit timestamps
        required created_at: datetime {
            default := datetime_of_transaction();
        }
    }

    type AuthorizationRequest {
        required user: User;

        required resource_type: ResourceType;
        required resource_id: str;

        required status: AuthStatus {
            default := AuthStatus.pending;
        }

        # request message
        message: str;
        # admin message
        response: str;

        # TODO: updated_by not implemented
        updated_by: int64;

        # Audit timestamps
        required created_at: datetime {
            default := datetime_of_transaction();
        }

        index on (.status);
    }
}
